group 'com.github.tmyroadctfig'
version '1.6'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

compileJava.options.debug = true
compileJava.options.debugOptions.debugLevel = "source,lines,vars"
compileJava.options.compilerArgs << "-Xlint:deprecation"

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.gson:gson:2.2.1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'log4j:log4j:1.2.17'
    compile 'commons-io:commons-io:2.2'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

def pomConfig = {
    groupId "$project.group"
    artifactId "$project.name"
    version "$version"

    name 'iCloud4j'
    description 'A library for accessing Apple\'s iCloud API'
    packaging 'jar'
    url 'https://github.com/tmyroadctfig/icloud4j'

    scm {
        connection 'scm:git:https://github.com/tmyroadctfig/icloud4j.git'
        url 'https://github.com/tmyroadctfig/icloud4j'
    }

    licenses {
        license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'tmyroadctfig'
            name 'Luke Quinane'
            email 'luke.quinane@gmail.com'
        }
    }
}

task createPom {
    doLast {
        pom {
            project pomConfig
        }.writeTo("$buildDir/pom.xml")
    }
}

jar {
    dependsOn createPom

    into("META-INF/maven/$project.group/$project.name") {
        from "$buildDir/pom.xml"
    }

    manifest {
        attributes(
            "Implementation-Title": "iCloud4j"
        )
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom {
                project pomConfig
            }
        }
    }
}